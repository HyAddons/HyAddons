/*
 * Modified from Skytils under GNU Affero Public license.
 * https://github.com/Skytils/SkytilsMod/blob/main/LICENSE
 * @author My-Name-Is-Jeff
 * @author Sychic
 */
allprojects {
    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
}
buildscript {
    repositories {
        jcenter()
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots"
        }
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
        maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath "com.github.asbyth:ForgeGradle:86b23926f3"
        classpath "com.github.Skytils:mixingradle:d75e32e743"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "org.spongepowered.mixin"
apply plugin: "java"
version = "pre3"
group = "com.jeromepaulos.hyaddons"
archivesBaseName = "HyAddons"

sourceCompatibility = targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
    clientJvmArgs += "-Dfml.coreMods.load=com.jeromepaulos.hyaddons.tweaker.FMLLoadingPlugin"
    clientRunArgs += "--mixin mixins.hyaddons.json"

}

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://repo.spongepowered.org/repository/maven-public/" }
    maven { url = "https://repo.sk1er.club/repository/maven-public/" }
}

configurations {
    packageLib
}

dependencies {
    implementation("club.sk1er:Vigilance:104-10809-SNAPSHOT")
    packageLib("club.sk1er:Vigilance:104-10809-SNAPSHOT")

    implementation("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        transitive = false
        exclude module: 'guava'
        exclude module: 'commons-io'
        exclude module: 'gson'
    }
    packageLib("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        exclude module: 'guava'
        exclude module: 'commons-io'
        exclude module: 'gson'
    }
    annotationProcessor("org.spongepowered:mixin:0.7.11-SNAPSHOT")
}

mixin {
    disableRefMapWarning = true
    defaultObfuscationEnv searge
    add sourceSets.main, "mixins.sre.refmap.json"
}
jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest.attributes(
            "FMLCorePlugin": "com.jeromepaulos.hyaddons.tweaker.FMLLoadingPlugin",
            "FMLCorePluginContainsFMLMod": true,
            "ForceLoadAsMod": true,
            "Main-Class": "HyAddonsInstallerFrame",
            "MixinConfigs": "mixins.hyaddons.json",
            "ModSide": "CLIENT",
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "TweakOrder": "0",
    )
    from {
        configurations.packageLib.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
sourceSets {
    main { output.resourcesDir = output.resourcesDir }
}